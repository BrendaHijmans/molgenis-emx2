plugins {
    id "jacoco"
    id "java"
}

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    repositories {
        mavenLocal()
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
    }

    /* default dependencies for all projects*/
    dependencies {

        implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.3'
        implementation 'org.apache.logging.log4j:log4j-core:2.13.3'
        implementation 'org.jooq:jooq:3.13.2'
        implementation 'com.zaxxer:HikariCP:3.4.5'
        implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.11.0'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'
        implementation 'com.fasterxml.jackson.core:jackson-core:2.11.0'
        implementation 'com.fasterxml.jackson.core:jackson-annotations:2.11.0'
        implementation 'com.graphql-java:graphql-java:15.0'

        //also used outside test
        implementation 'junit:junit:4.12'
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    sourceCompatibility = '11'

    jar {
        reproducibleFileOrder = true
        manifest {
            attributes(
                    'Specification-Version': project.version.toString(),
                    'Implementation-Version': getGitHash(),
                    'Created-By': "Gradle ${gradle.gradleVersion}",
                    'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                    'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
            )
        }
    }
}

/* generate java test coverage report*/
task jacocoMergedReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

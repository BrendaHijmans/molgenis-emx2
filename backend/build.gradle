plugins {
    id "jacoco"
    id "java"
}

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    repositories {
        mavenLocal()
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
    }

    /* default dependencies for all projects*/
    dependencies {
        implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.12.0'
        implementation 'org.apache.logging.log4j:log4j-core:2.11.1'
        testImplementation 'junit:junit:4.12'
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    sourceCompatibility = '11'
}

/* generate java test coverage report*/
task jacocoMergedReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}
